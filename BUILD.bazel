load("@bazel_lib//lib:transitions.bzl", "platform_transition_binary")
load("@rules_cc//cc:defs.bzl", "cc_binary")

package(default_visibility = ["//visibility:public"])

PLATFORMS = [
    "@toolchains_llvm_bootstrapped//platforms:macos_arm64",
    "@toolchains_llvm_bootstrapped//platforms:linux_amd64_musl",
    "@toolchains_llvm_bootstrapped//platforms:linux_arm64_musl",
    "@toolchains_llvm_bootstrapped//platforms:windows_amd64",
]

config_setting(
    name = "is_windows_clang_mingw",
    constraint_values = ["@platforms//os:windows"],
    flag_values = {"@rules_cc//cc/compiler:compiler": "clang"},
)

cc_binary(
    name = "pkgutil",
    srcs = ["pkgutil.c"],
    linkopts = select({
        ":is_windows_clang_mingw": [
            "-lbcrypt",
        ],
        "//conditions:default": [],
    }),
    deps = [
        "@libarchive//libarchive",
    ],
)

[
    platform_transition_binary(
        name = "for_" + platform.split(":")[1],
        binary = ":pkgutil",
        tags = ["manual"] + ["no-remote"] if platform == "@toolchains_llvm_bootstrapped//platforms:macos_arm64" else [],
        target_platform = platform,
    )
    for platform in PLATFORMS
]

filegroup(
    name = "for_all_platforms",
    srcs = [
        ":for_" + platform.split(":")[1]
        for platform in PLATFORMS
    ],
    tags = ["manual"],
)
